plugins {
  id "de.gesellix.docker" version "2015-07-05T16-55-10"
}
apply plugin: 'java'

version = '1.0.0'

repositories {
    jcenter()
}

dependencies {
    compile 'javax.servlet:servlet-api:2.5'
    compile 'commons-lang:commons-lang:2.6'
    compile ('edu.internet2.middleware.grouper:grouper:2.2.2') {
        transitive = false
    }
}

docker {
    dockerHost = System.env['DOCKER_HOST']?.replace("tcp", "http");
}

import de.gesellix.gradle.docker.tasks.*

task copyDocker(type: Copy) {
    from 'src/test/docker/'
    into 'build/docker-grouper/'
}

task copyJspToDocker(type: Copy) {
    dependsOn copyDocker

    from 'src/main/webapp/'
    into 'build/docker-grouper/grouper.ui-2.3.0/webapp'
}

task copyJavaToDocker(type: Copy) {
    dependsOn copyDocker

    from 'src/main/java/'
    into 'build/docker-grouper/grouper.ui-2.3.0/java/src/'
}

task buildImage(type: DockerBuildTask) {
    dependsOn copyJspToDocker, copyJavaToDocker, copyDocker

    imageName = "test/grouper-dev"
    buildContextDirectory = file('build/docker-grouper/')

    doFirst {
        logger.lifecycle("Building the initial Grouper image may take a long time. Have plenty of bandwidth.")
    }
}

task runContainer(type: DockerRunTask) {
    dependsOn buildImage

    imageName = "test/grouper-dev"
  	containerName = "grouper-dev"
  	containerConfiguration = [
      	"ExposedPorts": ["8080/tcp": [:]],
      	"HostConfig"  : [
           "Binds": [projectDir.toString() + "/build/libs:/jar-location"],
           "PortBindings": [
         		  "8080/tcp": [["HostPort": "8080"]],
              "5005/tcp": [["HostPort": "5005"]]
     		    ]
        ]
  	]
}

task stopContainer(type: DockerStopTask) {
  containerId = "grouper-dev"
}

task removeContainer(type: DockerRmTask) {
  dependsOn stopContainer

  containerId = "grouper-dev"
}

task resetContainer() {
    dependsOn removeContainer
}

clean {
    dependsOn resetContainer
}
